/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'kotlin-android'
    id 'com.android.library'
}

project.group = GROUP
version = VERSION_NAME

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    // We're on an old version of Robolectric which requires this, sadly.
    useLibrary 'org.apache.http.legacy'

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
    }

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
    }

    buildTypes {
        def internalBuildField = 'IS_INTERNAL_BUILD'
        def useIncrementalMountHelper = 'USE_INCREMENTAL_MOUNT_HELPER'

        debug {
            buildConfigField 'boolean', internalBuildField, 'true'
            buildConfigField 'boolean', useIncrementalMountHelper, 'true'
        }

        release {
            buildConfigField 'boolean', internalBuildField, 'false'
            buildConfigField 'boolean', useIncrementalMountHelper, 'true'
        }
    }

    // TODO(#62): Re-enable abort on error.
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Project dependencies
    api project(':litho-annotations')
    api project(':litho-rendercore')
    api project(':litho-rendercore-incremental-mount')
    api project(':litho-rendercore-visibility')
    api project(':litho-rendercore-transitions')

    // Annotations
    compileOnly deps.jsr305
    // Infer annotations is partially class-retained and need to be part
    // of the POM.
    implementation deps.inferAnnotations

    compileOnly deps.proguardAnnotations

    // Android Support Library
    compileOnly deps.supportAnnotations
    implementation deps.supportCore
    implementation deps.supportCustomView
    implementation deps.supportViewPager

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.6.0-rc01'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.3.0-beta01'
    // Animations
    implementation 'androidx.compose.animation:animation:1.3.0-beta01'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.3.0-beta01'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0-alpha01'
    // Runtime
    implementation 'androidx.compose.runtime:runtime:1.3.0-beta01'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.2.1'

    // First-party dependencies
    if (project.isRelease()) {
        api deps.yoga
    } else {
        api project(':yogajni')
    }
}

apply plugin: "com.vanniktech.maven.publish"
